WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

op_coord = { "+" }

op_mach = { id }

op_wire = { id }

opt_key = { id }

opt_val = { ASCII_DIGIT+ }

opt_tup = { opt_key ~ "=" ~ opt_val }

opt = { "[" ~ (opt_tup ~ ("," ~ opt_tup)*)? ~ "]" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

bel = { id }

coord = { "??" | id | ASCII_DIGIT+ }

expr_coord = { coord ~ (op_coord ~ coord)? }

loc = { bel? ~ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

instr_mach = { io ~ "=" ~ op_mach ~ opt ~ io ~ "@" ~ loc ~ ";" }

instr_wire = { io ~ "=" ~ op_wire ~ attr? ~ io? ~ ";" }

instr = { instr_mach | instr_wire }

file = { SOI ~ instr ~ EOI }