WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id | "_" }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

op_coord = { "+" }

bel = { id }

coord = { "??" | id | ASCII_DIGIT+ }

expr_coord = { coord ~ (op_coord ~ coord)? }

loc = { bel? ~ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

op_mach = { id }

op_wire = { id }

opt_key = { id }

opt_val = { ASCII_DIGIT+ }

opt_tup = { opt_key ~ "=" ~ opt_val }

opt = { "[" ~ (opt_tup ~ ("," ~ opt_tup)*)? ~ "]" }

instr_mach = { io ~ "=" ~ op_mach ~ opt ~ io ~ "@" ~ loc ~ ";" }

instr_wire = { io ~ "=" ~ op_wire ~ attr? ~ io? ~ ";" }

instr = { instr_mach | instr_wire }

body = { (instr ~ ("," ~ instr)*)? }

sig = { "def" ~ id ~ io? ~ "->" ~ io }

prog = { sig ~ "{" ~ body ~ "}" }

file = { SOI ~ prog ~ EOI }