WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

cost = @{ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

prim = { "??" | id }

hint = _{ "@" ~ prim }

op_comp = { "reg" | "add" | "sub" | "mul" | "not" | "and" | "or" | "xor" | "mux" | "eq" | "neq" | "gt" | "lt" | "ge" | "le" }

op_wire = { "id" | "const" | "sll" | "srl" | "sra" | "ext" | "cat" }

pat_op = _{ op_comp | op_wire }

pat_instr = { io ~ "=" ~ pat_op ~ attr? ~ io? ~ hint? ~ ";" }

pat_body = { pat_instr+ }

pat_sig = { "pat" ~ id ~ "[" ~ prim ~ "," ~ cost ~ "," ~ cost ~ "]" ~ io ~ "->" ~ io }

imp_sig = { "imp" ~ id ~ "[" ~ "]" ~ io ~ "->" ~ io }

pat = { pat_sig ~ "{" ~ pat_body ~ "}" }

imp = { imp_sig ~ "{" ~ "hello" ~ "}" }

des = { pat | imp }

body = { des+ }

target = { body }

file = { SOI ~ target ~ EOI }