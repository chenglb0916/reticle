WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

cost = @{ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

opt_key = { id }

dec_num = { ASCII_DIGIT+ }

bin_num = { ASCII_BIN_DIGIT+ }

hex_num = { ASCII_HEX_DIGIT+ }

opt_num = { "0b" ~ bin_num | "0x" ~ hex_num | dec_num }

opt_dsp = { id }

opt_val = _{ opt_num | opt_dsp }

opt_tup = { opt_key ~ "=" ~ opt_val }

opt = { "[" ~ (opt_tup ~ ("," ~ opt_tup)*)? ~ "]" }

prim = { "??" | id }

pat_hint = _{ "@" ~ prim }

bel = { id }

coord = { "??" | id | ASCII_DIGIT+ }

expr_coord = { coord ~ (op_coord ~ coord)? }

loc = { bel ~ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

imp_hint = _{ "@" ~ loc }

op_coord = { "+" }

op_comp = { "reg" | "add" | "sub" | "mul" | "not" | "and" | "or" | "xor" | "mux" | "eq" | "neq" | "gt" | "lt" | "ge" | "le" }

op_wire = { "id" | "const" | "sll" | "srl" | "sra" | "ext" | "cat" }

op_mach = { "lut1" | "lut2" | "lut3" | "lut4" | "lut5" | "lut6" | "dsp" | "carry" | "fdre" | "fdse" }

op_basc = { "id" | "gnd" | "vcc" | "ext" | "cat" }

pat_op = _{ op_comp | op_wire }

pat_instr = { io ~ "=" ~ pat_op ~ attr? ~ io? ~ pat_hint? ~ ";" }

pat_body = { pat_instr+ }

pat_sig = { "pat" ~ id ~ "[" ~ prim ~ "," ~ cost ~ "," ~ cost ~ "]" ~ io ~ "->" ~ io }

pat = { pat_sig ~ "{" ~ pat_body ~ "}" }

imp_op = _{ op_mach | op_basc }

imp_attr = _{ opt | attr }

imp_instr = { io ~ "=" ~ imp_op ~ imp_attr? ~ io ~ imp_hint? ~ ";" }

imp_body = { imp_instr+ }

imp_sig = { "imp" ~ id ~ "[" ~ coord ~ "," ~ coord ~ "]" ~ io ~ "->" ~ io }

imp = { imp_sig ~ "{" ~ imp_body ~ "}" }

des = { pat | imp }

body = { des+ }

target = { body? }

file = { SOI ~ target ~ EOI }