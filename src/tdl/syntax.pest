WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

cost = @{ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

prim = { "??" | id }

hint = _{ "@" ~ prim }

op_comp = { id }

op_wire = { id }

instr_comp = { io ~ "=" ~ op_comp ~ attr? ~ io ~ hint? ~ ";" }

instr_wire = { io ~ "=" ~ op_wire ~ attr? ~ io? ~ ";" }

instr = { instr_comp | instr_wire }

body = { (instr ~ ("," ~ instr)*)? }

sig = { "def" ~ id ~ "[" ~ prim ~ "," ~ cost ~ "," ~ cost ~ "]" ~ io ~ "->" ~ io }

def = { sig ~ "{" ~ body ~ "}" }

desc = { (def ~ ("," ~ def)*)? }

file = { SOI ~ desc ~ EOI }