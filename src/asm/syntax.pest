WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

prim = { id }

coord = { "??" | id | ASCII_DIGIT+ }

op_coord = { "+" }

expr_coord = { coord ~ (op_coord ~ coord)? }

hint = _{ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

loc = { "@" ~ prim ~ hint? }

op_asm = { id }

op_wire = { id }

instr_asm = { io ~ "=" ~ op_asm ~ io ~ loc ~ ";" }

instr_wire = { io ~ "=" ~ op_wire ~ attr? ~ io? ~ ";" }

instr = { instr_asm | instr_wire }

body = { instr+ }

sig = { "def" ~ id ~ io? ~ "->" ~ io }

prog = { sig ~ "{" ~ body ~ "}" }

file = { SOI ~ prog ~ EOI }