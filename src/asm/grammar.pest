WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

num = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

ty_int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

ty_bool = @{ ASCII_ALPHA_LOWER+ }

ty_vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty_prim = {
    "??" |
    "lut" |
    "dsp"
}

ty = { ty_vector | ty_int | ty_bool }

expr = { id ~ ":" ~ ty | id | num }

expr_coord = { "??" | id | num }

attrs = { "[" ~ (expr ~ ("," ~ expr)*)? ~ "]" }

params = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

loc = { "@" ~ ty_prim ~ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

op_phy = _{ id }

instr_phy = { expr ~ "=" ~ op_phy ~ attrs? ~ params ~ loc? ~ ";" }

op_std = {
    "id" |
    "const" |
    "shl" |
    "shr"
}

instr_std = { expr ~ "=" ~ op_std ~ attrs? ~ params? ~ ";" }

instr = { instr_phy | instr_std }

instrs = { instr ~ (instr)* }

inputs = { (expr ~ ("," ~ expr)*)? }

outputs = { (expr ~ ("," ~ expr)*)? }

prog = { "def" ~ id ~ "(" ~ inputs ~ ")" ~ "->" ~ "(" ~ outputs ~ ")" ~ "{" ~ instrs ~ "}" }

file = { SOI ~ prog ~ EOI }