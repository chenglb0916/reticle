WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

prim = { "??" | id }

hint = _{ "@" ~ prim }

op_comp = { id }

op_wire = { id }

op_call = { id }

instr_comp = { io ~ "=" ~ op_comp ~ attr? ~ io ~ hint? ~ ";" }

instr_wire = { io ~ "=" ~ op_wire ~ attr? ~ io? ~ ";" }

instr_call = { io ~ "=" ~ op_call ~ io? ~ ";" }

instr = { instr_comp | instr_wire | instr_call }

body = { instr ~ (instr)* }

sig = { "def" ~ id ~ io? ~ "->" ~ io }

def = { sig ~ "{" ~ body? ~ "}" }

prog = { def ~ (def)* }

file = { SOI ~ prog ~ EOI }