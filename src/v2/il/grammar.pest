WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

id = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

name = { id ~ ":" ~ ty | id }

tup_name = { (name ~ ("," ~ name)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { name | "(" ~ tup_name ~ ")" }

attrs = _{ "[" ~ tup_val ~ "]" }

resrc = { "??" | id }

hint = _{ "@" ~ resrc }

instr = { io ~ "=" ~ id ~ attrs? ~ io? ~ hint? ~ ";" }

body = { (instr ~ ("," ~ instr)*)? }

sig = { id ~ io? ~ "->" ~ io }

def = { sig ~ "{" ~ body ~ "}" }

prog = { (def ~ ("," ~ def)*)? }

file = { SOI ~ prog ~ EOI }