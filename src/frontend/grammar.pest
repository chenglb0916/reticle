WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

identifier = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

number = @{ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = { vector | int | bool }

expr = { identifier | number }

primop = {
    "reg" |
    "add" |
    "sub" |
    "mul" |
    "not" |
    "and" |
    "nand" |
    "or" |
    "nor" |
    "xor" |
    "xnor" |
    "mux" |
    "eq" |
    "neq" |
    "gt" |
    "lt" |
    "ge" |
    "le"
}

stdop = {
    "id" |
    "const"
}

op = _{ primop | stdop }

attrs = { "[" ~ (expr ~ ("," ~ expr)*)? ~ "]" }

params = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

instr = { identifier ~ ":" ~ ty ~ "=" ~ op ~ attrs? ~ params? ~ ";" }

instrs = { instr ~ (instr)* }

input = { identifier ~ ":" ~ ty }

inputs = { "(" ~ (input ~ ("," ~ input)*)? ~ ")" }

output = { identifier ~ ":" ~ ty }

outputs = { "->" ~ "(" ~ (output ~ ("," ~ output)*)? ~ ")" }

def = {
    "def" ~ identifier ~ inputs ~ outputs ~ "{" ~ instrs ~ "}"
}

file = { SOI ~ def+ ~ EOI }