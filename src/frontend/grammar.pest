WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sep = _{ "_" | "-"}

identifier = @{ ASCII_ALPHA+ ~ (sep | ASCII_ALPHA | ASCII_DIGIT)* }

number = @{ ASCII_DIGIT+ }

ty_int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

ty_bool = @{ ASCII_ALPHA_LOWER+ }

ty = { ty_int | ty_bool }

expr = { identifier }

primop = { "reg" | "add" | "sub" | "mul" }

stdop = { "id" }

op = _{ primop | stdop }

params = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

instr = { identifier ~ ":" ~ ty ~ "=" ~ op ~ params ~ ";" }

instrs = { instr ~ (instr)* }

input = { identifier ~ ":" ~ ty }

inputs = { "(" ~ (input ~ ("," ~ input)*)? ~ ")" }

output = { identifier ~ ":" ~ ty }

outputs = { "->" ~ "(" ~ (output ~ ("," ~ output)*)? ~ ")" }

def = {
    "def" ~ identifier ~ inputs ~ outputs ~ "{" ~ instrs ~ "}"
}

file = { SOI ~ def+ ~ EOI }