WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

id = @{ ASCII_ALPHA+ ~ (ASCII_ALPHA | ASCII_DIGIT)* }

val = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

int = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ }

bool = @{ ASCII_ALPHA_LOWER+ }

vector = @{ ASCII_ALPHA_LOWER ~ ASCII_DIGIT+ ~ "<" ~ ASCII_DIGIT+ ~ ">" }

ty = ${ vector | int | bool }

var = { id ~ ":" ~ ty | id | "_" }

tup_var = { (var ~ ("," ~ var)*)? }

tup_val = { (val ~ ("," ~ val)*)? }

io = { var | "(" ~ tup_var? ~ ")" }

attr = _{ "[" ~ tup_val ~ "]" }

op_coord = { "+" }

bel = { id }

coord = { "??" | id | ASCII_DIGIT+ }

expr_coord = { coord ~ (op_coord ~ coord)? }

loc = { bel ~ "(" ~ expr_coord ~ "," ~ expr_coord ~ ")" }

hint = _{ "@" ~ loc }

op_mach = { id }

op_basc = { id }

opt_key = { id }

dec_num = { ASCII_DIGIT+ }

bin_num = { ASCII_BIN_DIGIT+ }

hex_num = { ASCII_HEX_DIGIT+ }

opt_num = { "0b" ~ bin_num | "0x" ~ hex_num | dec_num }

opt_op = { id }

opt_val = _{ opt_num | opt_op }

opt_tup = { opt_key ~ "=" ~ opt_val }

opt = { "[" ~ (opt_tup ~ ("," ~ opt_tup)*)? ~ "]" }

instr_mach = { io ~ "=" ~ op_mach ~ opt? ~ io ~ hint? ~ ";" }

instr_basc = { io ~ "=" ~ op_basc ~ attr? ~ io ~ ";" }

instr = { instr_basc | instr_mach }

body = { instr+ }

sig = { "def" ~ id ~ io? ~ "->" ~ io }

prog = { sig ~ "{" ~ body ~ "}" }

file = { SOI ~ prog ~ EOI }